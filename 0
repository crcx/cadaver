#!/home/crc/local/bin/cyanide

"Cadaver: A Weblog Engine in Parable"
"-----------------------------------------------------------------------------"

[ 'http://api.forthworks.com/cadaver/0' ] 'BASE-HREF' define

"-----------------------------------------------------------------------------"

[ '*Post'  '*I' ] {

  [ "n-s"  :s dup length? 2 - 0 swap subslice :s ] 'strip-suffix' define
  [ "n-s"  :s 'cache/' swap + strip-suffix '.html' + ] 'get-filename' define
  [ "-n"   'mostRecent' slurp-file dup length? 0 swap subslice :s :n ] 'newest-post' define

  [ "n-s" \
    strip-suffix to *I \
    [ '<h3><a href="' BASE-HREF $/ *I '">Post #' *I '</a></h3>' ] build-string \
  ] 'permalink' define

  [ "n-" \
    [ permalink ] sip \
    get-filename slurp-file \
  ] 'display-post' define

"-----------------------------------------------------------------------------"
[ '*Renderer' ] {
  [ "q-" \
    to *Renderer \
    'text/html' content-type \
    [ '<!DOCTYPE html>' \
      [ \
        [ \
          [ 'Cadaver: a Weblog' ] title \
          [ 'https://maxcdn.bootstrapcdn.com/bootstrap/3.3.6/css/bootstrap.min.css' ] stylesheet \
          '<meta name="viewport" content="width=device-width, initial-scale=1">' \
        ] head \
        [ [ 'container' ] [ *Renderer invoke '<hr>' [ '&copy; 2015, Charles Childers' ] p ] div:class ] body \
      ] html \
    ] build-string println \
  ] 'display-page' define
}

"-----------------------------------------------------------------------------"

"Handlers for various Paths"
  [ "-..." \
    [ 'The most recent posts are...' ] h1 \
    newest-post to *Post \
    *Post 3 min [ '<hr>' *Post display-post *Post 1 - to *Post ] times \
  ] '/recent' define

  [ "-..." \
    PATH_INFO rest :s :n display-post \
  ] '/permalink' define

  [ "-" \
    "TODO: 404 Error page" \
  ] '/404' define
}

"-----------------------------------------------------------------------------"

"Generate the page"

[ "-" \
  [ [ [        -path? ] [ /recent ] ] \
    [ [ '/404'  path? ] [ /404 ] ] \
    [ [ '/'     path? ] [ /404 ] ] \
    [ [ PATH_INFO @ $/  eq? ] [ /permalink ] ] \
    [ [ true ] [ 'error!' ] ] \
  ] when \
] 'handle-request' define


[ handle-request ] display-page
